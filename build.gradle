String gitVersion
if (hasProperty("release")) {
	gitVersion = [ "git", "describe", "--match", "[0-9]*", "--dirty"].execute().text.trim()
} else {
	gitVersion = [ "git", "describe", "--match", "[0-9]*", "--abbrev=0"].execute().text.trim() + "-SNAPSHOT"
}

task version {
	doLast {
		println "Version: ${gitVersion}"
	}
}

def sonatype = hasProperty("sonatype")
def signArtifacts = sonatype || hasProperty("sign")

allprojects {
	group = "com.prezi.gradle.pride"
	version = gitVersion
	apply plugin: "idea"
}

subprojects { subproject ->
	apply plugin: "maven"
	apply plugin: "groovy"
	sourceCompatibility = 1.6
	targetCompatibility = 1.6

	description = "Pride is a tool to manage a pride of Gradle-built modules stored in different GitHub repositories"

	repositories {
		mavenLocal()
		mavenCentral()
		maven {
			url "http://gradle.artifactoryonline.com/gradle/libs"
		}
	}

	dependencies {
		testCompile "junit:junit:4.11"
		testCompile("org.spockframework:spock-core:0.7-groovy-1.8") {
			exclude group: "org.codehaus.groovy"
		}
		testRuntime "org.slf4j:slf4j-simple:1.7.7"
	}

	uploadArchives {
		repositories {
			mavenDeployer {
				pom.project {
					url "http://github.com/prezi/pride"
					name subproject.name
					description subproject.description

					scm {
						url "scm:git@github.com:prezi/pride.git"
						connection "scm:git@github.com:prezi/pride.git"
						developerConnection "scm:git@github.com:prezi/pride.git"
					}

					licenses {
						license {
							name "The Apache Software License, Version 2.0"
							url "http://www.apache.org/licenses/LICENSE-2.0.txt"
							distribution "repo"
						}
					}

					developers {
						developer {
							id "lptr"
							name "Lorant Pinter"
						}
					}
				}

				if (sonatype) {
					repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
						def user = project.getProperty("sonatypeUsername")
						def password = project.getProperty("sonatypePassword")
						authentication(userName: user, password: password)
					}
					snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
						def user = project.getProperty("sonatypeUsername")
						def password = project.getProperty("sonatypePassword")
						authentication(userName: user, password: password)
					}
				} else if (project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")) {
					def user = project.getProperty("nexusUser")
					def password = project.getProperty("nexusPassword")
					repository(url: "https://artifactory.prezi.com/plugins-release-local/") {
						authentication(userName: user, password: password)
					}
					snapshotRepository(url: "https://artifactory.prezi.com/plugins-snapshot-local/") {
						authentication(userName: user, password: password)
					}
				}
			}
		}
	}

	if (signArtifacts) {
		apply plugin: "signing"

		signing {
			sign configurations.archives
		}

		uploadArchives {
			repositories {
				mavenDeployer {
					beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
				}
			}
		}
	}

	task groovydocJar(type: Jar, dependsOn: groovydoc) {
		classifier = "javadoc"
		from "build/docs/groovydoc"
	}

	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = "sources"
	}

	jar {
		from rootProject.file("LICENSE.txt")
	}

	artifacts {
		archives jar
		archives groovydocJar
		archives sourcesJar
	}
}

project("pride-core") {
	dependencies {
		compile "org.codehaus.groovy:groovy:1.8.6"
		compile "commons-configuration:commons-configuration:1.10", {
			exclude group: "commons-logging"
		}
		compile "org.slf4j:jcl-over-slf4j:1.7.7"
	}

	processResources {
		inputs.property "version", version
		filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
			version: version
		]
	}
}

project("pride-git-support") {
	dependencies {
		compile project(path: ":pride-core")
	}
}

project("pride-svn-support") {
	dependencies {
		compile project(path: ":pride-core")
	}
}

project("pride") {
	apply plugin: "application"
	mainClassName = "com.prezi.gradle.pride.cli.PrideCli"

	dependencies {
		compile project(path: ":pride-core")
		compile project(path: ":pride-git-support")
		compile project(path: ":pride-svn-support")
		compile "org.gradle:gradle-tooling-api:1.12"
		compile "io.airlift:airline:0.6"
		compile "ch.qos.logback:logback-classic:1.1.2"
	}

	configure(applicationDistribution) {
		from(rootProject.file("README.md")) {
			into "/"
		}
		from(rootProject.file("LICENSE.txt")) {
			into "/"
		}
	}

	artifacts {
		archives(distZip) {
			classifier "dist"
		}
	}
}

project("gradle-pride-plugin") {
	dependencies {
		compile gradleApi()
		compile project(":pride-core"), {
			exclude group: "org.codehaus.groovy"
		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
